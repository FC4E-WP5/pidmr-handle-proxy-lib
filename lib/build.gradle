plugins {
    id 'java-library'
}

group = 'eu.faircore4eosc'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://handle.net/maven' }
}

ext {
    handleVersion = '9.3.1'
    servletApiVersion = '3.1.0'
    influxdbVersion = '2.24'
    javaVersion = 21
}

dependencies {
    implementation "net.handle:handle:$handleVersion"
    implementation "javax.servlet:javax.servlet-api:$servletApiVersion"
    implementation "org.influxdb:influxdb-java:$influxdbVersion"
    implementation "org.json:json:20230227"
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation "com.fasterxml.jackson.core:jackson-databind:2.15.2"
    implementation "com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20240325.1"
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    archiveBaseName.set("PIDMRHDLProxy")
    archiveVersion.set(version)
}

task influxdb(type: Jar) {
    archiveBaseName.set("influxdb")
    archiveVersion.set(influxdbVersion)

    dependsOn compileJava

    from(sourceSets.main.output) {
        exclude 'eu/**'
    }

    from {
        configurations.runtimeClasspath.findAll {
            !(it.name.contains("handle") || it.name.contains("cnri") || it.name.contains("servlet") || it.name.contains("pidmr"))
        }.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.build.dependsOn influxdb

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked']
    options.compilerArgs << "-Xlint:deprecation"
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

gradle.settingsEvaluated {
    startParameter.enableConfigurationCache = true
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
    }
    test {
        java.srcDirs = ['src/test/java']
    }
}
